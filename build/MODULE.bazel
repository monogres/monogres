module(
    name = "monogres",
    # https://bazel.build/external/faq#module-versioning-best-practices
    version = "",
    bazel_compatibility = [">=7.3.1"],
    # https://bazel.build/external/faq#incrementing-compatibility-level
    compatibility_level = 0,
)

bazel_dep(name = "rules_bison", version = "0.4")
bazel_dep(name = "rules_flex", version = "0.4")
bazel_dep(name = "rules_foreign_cc", version = "0.14.0")
bazel_dep(name = "rules_m4", version = "0.3")
bazel_dep(name = "rules_python", version = "1.0.0")

bazel_dep(name = "toolchains_llvm", version = "1.4.0", dev_dependency = True)

# toolchains/
llvm = use_extension("@toolchains_llvm//toolchain/extensions:llvm.bzl", "llvm", dev_dependency = True)
llvm.toolchain(
    name = "llvm_toolchain_system",
    llvm_version = "14.0.6",
)
llvm.toolchain_root(
    name = "llvm_toolchain_system",
    path = "/usr/lib/llvm-14",
)
use_repo(llvm, "llvm_toolchain_system")

register_toolchains(
    "@llvm_toolchain_system//:all",
    dev_dependency = True,
)

bison = use_extension(
    "@rules_bison//bison/extensions:bison_repository_ext.bzl",
    "bison_repository_ext",
)
bison.repository(
    name = "bison",
    version = "3.3.2",
)
use_repo(bison, "bison")

flex = use_extension("@rules_flex//flex/extensions:flex_repository_ext.bzl", "flex_repository_ext")
flex.repository(
    name = "flex",
    version = "2.6.4",
)
use_repo(flex, "flex")

m4 = use_extension("@rules_m4//m4/extensions:m4_repository_ext.bzl", "m4_repository_ext")
m4.repository(
    name = "m4",
    version = "1.4.18",
)
use_repo(m4, "m4")

python = use_extension("@rules_python//python/extensions:python.bzl", "python")
python.toolchain(
    python_version = "3.11",
)
use_repo(python, "python_3_11")

# postgres/
http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

BUILD_REPO = """
filegroup(
    name = "files",
    srcs = glob(["**"]),
    visibility = ["//visibility:public"],
)

alias(
    name = "{name}",
    actual = ":files",
    visibility = ["//visibility:public"],
)
"""

NAME = "pg_src--16.0"

TAG = "REL_16_0"

URL = "https://github.com/postgres/postgres/archive/refs/tags/{}.tar.gz".format(TAG)

SHA256 = "f3ffaa5cbeefd3a6d426423e1001d01e543841946e5b13d4c8ebcad4434f2be8"

http_archive(
    name = NAME,
    build_file_content = BUILD_REPO.format(name = NAME),
    patch_args = ["-p1"],
    patches = [
        "//postgres/patches:0001-16.0-fix-pgflex-propagate-environment-to-flex-subprocess.patch",
        "//postgres/patches:0002-16.0-fix-pgflex-suppress-line-directives.patch",
        "//postgres/patches:0003-16.0-fix-pgflex-flex-2.6.4-noline-bugs.patch",
        "//postgres/patches:0004-16.0-feat-prefix_distro-option.patch",
        "//postgres/patches:0005-16.0-hack-remove-execroot-from-CC.patch",
    ],
    sha256 = SHA256,
    strip_prefix = "postgres-{}".format(TAG),
    url = URL,
)
